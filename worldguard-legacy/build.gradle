apply plugin: 'eclipse'
apply plugin: 'idea'

repositories {
    maven {
        name = 'bstats repo'
        url = 'https://repo.codemc.org/repository/maven-public'
    }
}

dependencies {
    compile project(path: ':worldguard-core', configuration: 'shadow')
    compile 'org.bukkit:bukkit:1.13-R0.1-SNAPSHOT'
    compile 'com.sk89q.worldedit:worldedit-bukkit:7.0.0-SNAPSHOT'
    compile ('com.sk89q:commandbook:2.3') {
        exclude group: 'com.sk89q', module: 'worldedit'
        exclude group: 'com.zachsthings.libcomponents'
    }
    compile 'org.bstats:bstats-bukkit:1.4'
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        expand project.properties
        include 'plugin.yml'
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}

jar {
    manifest {
        attributes("Implementation-Version": version)
    }
}

shadowJar {
    dependencies {
        include(dependency(':worldguard-core'))
        relocate ("org.bstats", "com.sk89q.worldguard.bukkit.bstats") {
            include(dependency("org.bstats:bstats-bukkit:1.4"))
        }
    }

    relocate('org.flywaydb', 'com.sk89q.worldguard.internal.flywaydb')
    relocate('org.json.simple', 'com.sk89q.worldguard.util.jsonsimple')

}

build.dependsOn(shadowJar)
